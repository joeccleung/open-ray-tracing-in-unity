#ifndef RTMeshBVH_Intersect_Compute
#define RTMeshBVH_Intersect_Compute

#include "../../BVH/RayBoxIntersect.compute"

// [intersect(RTMeshBVH)]

StructuredBuffer<float> _RTMeshBVH;
StructuredBuffer<int> _RTMeshBVHGeometryMapping;
StructuredBuffer<float> _RTMeshBVHGeometryData;

void RTMeshBVHIntersect(Ray ray, inout RayHit bestHit, Primitive primitive, int primitiveId)
{
    int NO_CHILD = -1;

    int bvhStart = 1;   // It always start with 1 (the [0] holds the size of the BVH)

    int BOX_LEFT = 0;
    int BOX_RIGHT = 1;
    int BOX_MAX_X = 2;
    int BOX_MAX_Y = 3;
    int BOX_MAX_Z = 4;
    int BOX_MIN_X = 5;
    int BOX_MIN_Y = 6;
    int BOX_MIN_Z = 7;

    int BOX_STRIDE = 8;
    int TRIANGLE_STRIDE = 20;   // Different from CPU debug ray project by 3 normals

    // First, we have to jump to the start of our BVH
    [fastopt] for(int s = 0; s < primitive.geometryInstanceBegin; s++)
    {
        bvhStart += _RTMeshBVH[bvhStart-1] + 1;
    }

    int bvhStack[32];
    bvhStack[0] = bvhStart;
    int bvhHead = 0;

    [fastopt] while (true)
    {
        if (bvhHead <= -1) 
        {
            // No more node to traverse
            break;
        }

        int nodeIdx = bvhStack[bvhHead];
        bvhHead--;

        if (RayBoxIntersection(ray, 
                               float3(_RTMeshBVH[nodeIdx + BOX_MIN_X], _RTMeshBVH[nodeIdx + BOX_MIN_Y], _RTMeshBVH[nodeIdx + BOX_MIN_Z]), 
                               float3(_RTMeshBVH[nodeIdx + BOX_MAX_X], _RTMeshBVH[nodeIdx + BOX_MAX_Y], _RTMeshBVH[nodeIdx + BOX_MAX_Z]))) 
        {
            if (_RTMeshBVH[nodeIdx + BOX_LEFT] == NO_CHILD) {
                
                // Leaf node, ray-triangle intersection

                // If left child index indicates a leaf node (i.e. == NO_CHILD), right child index will hold the index to the local AccGeoMap
                int boxGeoMapIndex = (int)_RTMeshBVH[nodeIdx + BOX_RIGHT];

                // The first item of the bounding box holds the number of geometries
                int numberOfGeoInBox = _RTMeshBVHGeometryMapping[boxGeoMapIndex];

                // The geometries index list started after the item that holds the number of geometry in the box
                int boxGeoMapListCursor = boxGeoMapIndex + 1;

                [fastopt] for (int geo = 0; geo < numberOfGeoInBox; geo++)
                {
                    int geoCursor = _RTMeshBVHGeometryMapping[boxGeoMapListCursor];

                    float3 vertices1[3];
                    vertices1[0] = float3(_RTMeshBVHGeometryData[geoCursor + 0], _RTMeshBVHGeometryData[geoCursor + 1], _RTMeshBVHGeometryData[geoCursor + 2]);
                    vertices1[1] = float3(_RTMeshBVHGeometryData[geoCursor + 3], _RTMeshBVHGeometryData[geoCursor + 4], _RTMeshBVHGeometryData[geoCursor + 5]);
                    vertices1[2] = float3(_RTMeshBVHGeometryData[geoCursor + 6], _RTMeshBVHGeometryData[geoCursor + 7], _RTMeshBVHGeometryData[geoCursor + 8]);
                    float3 normal0 = float3(_RTMeshBVHGeometryData[geoCursor + 9], _RTMeshBVHGeometryData[geoCursor + 10], _RTMeshBVHGeometryData[geoCursor + 11]);
                    float3 normal1 = float3(_RTMeshBVHGeometryData[geoCursor + 12], _RTMeshBVHGeometryData[geoCursor + 13], _RTMeshBVHGeometryData[geoCursor + 14]);
                    float3 normal2 = float3(_RTMeshBVHGeometryData[geoCursor + 15], _RTMeshBVHGeometryData[geoCursor + 16], _RTMeshBVHGeometryData[geoCursor + 17]);
                    float2 uv0 = float2(_RTMeshBVHGeometryData[geoCursor + 18], _RTMeshBVHGeometryData[geoCursor + 19]);
                    float2 uv1 = float2(_RTMeshBVHGeometryData[geoCursor + 20], _RTMeshBVHGeometryData[geoCursor + 21]);
                    float2 uv2 = float2(_RTMeshBVHGeometryData[geoCursor + 22], _RTMeshBVHGeometryData[geoCursor + 23]);

                    float4 result = TriangleIntersectUtils(ray, bestHit.distance, vertices1, primitiveId);

                    if (result.x != -1) // Has intersect?
                    {
                        bestHit.barycentric = float3(result.z, result.w, 1-result.z-result.w);
                        bestHit.distance = result.x;
                        bestHit.position = ray.origin + result.x * ray.direction;
                        bestHit.primitiveId = primitiveId;

                        if (result.y <= 0)  // Ray is inside->outside?
                        {
                            // Ray is inside->outside
                            bestHit.mediumToEnter = 0; // FIXME: Even if an inside ray is leaving, it may not necessary entering the air
                            bestHit.mediumToLeave = primitiveId;
                            bestHit.normal = bestHit.barycentric.x * -1 * normal1 + bestHit.barycentric.y * -1 * normal2 + bestHit.barycentric.z * -1 * normal0;
                            bestHit.uv = bestHit.barycentric.x * uv1 + bestHit.barycentric.y * uv2 + bestHit.barycentric.z * uv0;
                        }
                        else
                        {
                            bestHit.mediumToEnter = primitiveId;  // Ray is outside->inside
                            bestHit.mediumToLeave = ray.medium;
                            bestHit.normal = bestHit.barycentric.x * normal1 + bestHit.barycentric.y * normal2 + bestHit.barycentric.z * normal0;
                            bestHit.uv = bestHit.barycentric.x * uv1 + bestHit.barycentric.y * uv2 + bestHit.barycentric.z * uv0;
                        }
                    }

                    boxGeoMapListCursor++;
                }
            }
            else
            {
                // Intermediate node
                bvhHead++;
                bvhStack[bvhHead] = bvhStart + _RTMeshBVH[nodeIdx + BOX_RIGHT] * BOX_STRIDE;

                bvhHead++;
                bvhStack[bvhHead] = bvhStart + _RTMeshBVH[nodeIdx + BOX_LEFT] * BOX_STRIDE;
            }
        }
    }
}

#endif